Name        NabuAdapter_800-11887;
PartNo      XXXXX;
Date        XX/XX/XX;
Revision    XX;
Designer    Reverse engineered by Phil Pemberton;
Company     NABU Network Inc.;
Assembly    NABU Adaptor main PCB;
Location    U5;
Device      PAL16R6;

/*  Pin definitions  */

PIN  1 = CLOCK;				/* Byte clock							*/
PIN  2 = D7;				/*  \									*/
PIN  3 = D6;				/*   }									*/
PIN  4 = D5;				/*   }									*/
PIN  5 = D4;				/*   } Data from cable TV				*/
PIN  6 = D3;				/*   } after destuffing					*/
PIN  7 = D2;				/*   }									*/
PIN  8 = D1;				/*   }									*/
PIN  9 = D0;				/*  /									*/
/* PIN 10 = GND;  */		/* Ground								*/
/* PIN 11 = /OE; */			/* Registered output enable				*/
PIN 12 = RXGATE_REQ_Q;	/* Input from MCU pin PC0. 1=MCU requests cable receive to fifo */
PIN 13 = MCU_PC2;			/* Output to MCU pin PC2				*/
PIN 14 = MCU_PC3;			/* Output to MCU pin PC3				*/
PIN 15 = iUniqAddrDet;		/* internal signal: unique address detect	*/
PIN 16 = iBcastAddrDet;				/* internal signal: broadcast address detect	*/
PIN 17 = FIFO_SO_SET;		/* Fifo SO logic clock, L->H edge triggered		*/
PIN 18 = nFIFO_SO_CLR;		/* Fifo SO logic reset, active low		*/
PIN 19 = nHDLC_FLAG_Q;		/* HDLC FLAG detect, qualified			*/


/* Data input bus from cable TV network. */
/* Has been de-zero-stuffed and flag-synchronised. */
FIELD Data = [D7..D0];

/* Sequence count: preset to 11,  */
FIELD State = [FIFO_SO_SET, nFIFO_SO_CLR];


/* All registers are set when any of these terms are true */
common_set =
    (State:3 & !iBcastAddrDet &  MCU_PC3) #
    (State:3 & !iUniqAddrDet & !MCU_PC2) #
    (!iBcastAddrDet &  MCU_PC2) #
    (!iUniqAddrDet & !MCU_PC3) # 
    (!iBcastAddrDet & !iUniqAddrDet);			/* Detection of both broadcast and local address */


/* Pin 18, to U28A /RESET.  0 = deactivate FIFO Shift Out signal.  */
nFIFO_SO_CLR.D =
    (!RXGATE_REQ_Q & nFIFO_SO_CLR) #		/* 1 (no clear) if pin high, and RXGATE_REQ is low */
    (nHDLC_FLAG_Q & nFIFO_SO_CLR) #			/* 1 (no clear) if pin high, and FLAG detected */
    common_set;

/* Pin 17, to U29A CLOCK.  L->H = latch U7.LE into U29; latch gate */
FIFO_SO_SET.D =
    (!RXGATE_REQ_Q & State:1) #
    (!RXGATE_REQ_Q & State:2) #
    (nHDLC_FLAG_Q  & State:1) #		/* HDLC FLAG + FIFO SO gate clear */
    (nHDLC_FLAG_Q  & State:2) #		/* HDLC FLAG + not FIFO SO gate set */
    common_set;

/* Internal signal, no external connection. Used to detect the broadcast address. */
iBcastAddrDet.D =
    (Data:'h'FF   & nHDLC_FLAG_Q & State:3 & !iUniqAddrDet) #
    (Data:'h'FF   & nHDLC_FLAG_Q           & !iBcastAddrDet) #
    common_set;

/* Internal signal, no external connection. Used to detect the unique address. */
iUniqAddrDet.D =
    (Data:'h'80    & State:3 & !iUniqAddrDet) #					/* Address first byte */
    (Data:'h'01    & State:2 & !iUniqAddrDet) #					/* Address second byte */
    (Data:'h'18    & State:1 & !iUniqAddrDet) #					/* Address third byte */
    (!nHDLC_FLAG_Q &  RXGATE_REQ_Q) #		/* FLAG detected, with REQ set */
    common_set;

/* To microcontroller PC3 */
MCU_PC3.D =
    (Data:'h'87   & nHDLC_FLAG_Q & State:0 & !iUniqAddrDet) #		/* Address final byte */
    (Data:'h'FF   & nHDLC_FLAG_Q & State:0 & !iBcastAddrDet) #		/* All 1's -> broadcast? */
    (nHDLC_FLAG_Q  & !MCU_PC3) #
    (!RXGATE_REQ_Q & !MCU_PC3) #
    common_set;

/* To microcontroller PC2 */
MCU_PC2.D =
    (D7 & nHDLC_FLAG_Q & State:3 & !iUniqAddrDet) #
    (nHDLC_FLAG_Q & !MCU_PC2) #
    (!RXGATE_REQ_Q & !MCU_PC2) #
    common_set;

/* 14   13
 * PC3  PC2    State
 *  0    0     Broadcast or Local address detected
 *  0    1     ???
 *  1    0     Receiving address
 *  1    1     Waiting
 */

